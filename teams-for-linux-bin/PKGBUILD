# Maintainer: Ismael Martinez

pkgname=teams-for-linux
pkgver=0.1.9
pkgrel=1
pkgdesc='Unofficial Microsoft Teams client for Linux using Electron.'
arch=('any')
url='https://github.com/IsmaelMartinez/teams-for-linux'
license=('GPLv3')
depends=('nodejs' 'xdg-utils' 'electron' 'npm' 'yarn')
provides=("${pkgname}")
conflicts=("${pkgname}")
options=(!strip)
install="${pkgname}.install"
source=("https://github.com/IsmaelMartinez/teams-for-linux/archive/v${pkgver}.tar.gz"
        "${pkgname}.desktop"
        "${pkgname}.sh"
        "icon-96x96-monochrome.png")
sha256sums=('9bd286226e25753365850a50c69d574ad5789105c062b3332c88dc62e6d0e0e7'
            '47632a81e96fceadaa7080eeddb11b42e06e51f6002290435b0a4a635d355297'
            '332b041b5ca5fcc84fe16b7046880e0b7153e0a79f04b3bece67013ce2a03ed2'
            'ff4e698f7c79e96cf820b1f10bf0e5199c9dad2a533ef7b4a4b1f1ca03fcad2c')

prepare() {
    # patch pakcage.json (replace yarn install with yarn check since we cannot run yarn after install without root)
    # besides, we already run yarn insall during package installation and upgrade
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i "s/yarn install/yarn check/" package.json
}

package() {
    # install application
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -dm755 "${pkgdir}/opt/${pkgname}"
    cp -r --preserve=mode . "${pkgdir}/opt/${pkgname}"
    cp -f --preserve=mode "${srcdir}/icon-96x96-monochrome.png" "${pkgdir}/opt/${pkgname}/app/lib/assets/icons/icon-96x96.png"

    # install icons
    cd "${srcdir}/${pkgname}-${pkgver}/build/icons"
    for icon in *.png
    do
        iconname=$(basename "${icon}")
        dimensions="${iconname%.*}"
        install -Dm644 "${srcdir}/${pkgname}-${pkgver}/build/icons/${icon}" "${pkgdir}/usr/share/icons/hicolor/${dimensions}/apps/${pkgname}.png"
    done

    # install executable
    install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

    # install license
    install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"

    # install desktop entry
    install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
