# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=flashplugin-beta
_licensefile='PlatformClients_PC_WWEULA-MULTI-20110809_1357.pdf'
pkgver=23.0beta
pkgrel=1
pkgdesc='Adobe Flash Player Beta NPAPI plugin'
url='http://labs.adobe.com/downloads/flashplayer.html'
arch=('i686' 'x86_64')
depends=('mozilla-common' 'libxt' 'libxpm' 'gtk2' 'nss' 'curl')
optdepends=('libvdpau: GPU acceleration on Nvidia card')
provides=('flashplayer' 'flashplugin')
conflicts=('flashplugin')
license=('custom')
options=(!strip)
source=(http://www.adobe.com/products/eulas/pdfs/${_licensefile})
source_i686=(libflashplayer.i386.so::"https://fpdownload.macromedia.com/pub/labs/flashruntimes/flashplayer/linux32/libflashplayer.so")
source_x86_64=(libflashplayer.x86_64.so::"https://fpdownload.macromedia.com/pub/labs/flashruntimes/flashplayer/linux64/libflashplayer.so")
md5sums=('620a140c7e85af655f39f1b583fbf932')
md5sums_i686=('07cfa460fdae12843b63dad83fd9058c')
md5sums_x86_64=('24c8c35fb0d72c11cfa19e7997007e9b')

package () {
    if [ "$CARCH" = x86_64 ]; then
        install -Dm755 libflashplayer.x86_64.so "$pkgdir/usr/lib/mozilla/plugins/libflashplayer.so"
    else
        install -Dm755 libflashplayer.i386.so "$pkgdir/usr/lib/mozilla/plugins/libflashplayer.so"
    fi
    install -Dm644 "${_licensefile}" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.pdf"
}
