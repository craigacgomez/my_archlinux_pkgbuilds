# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-openconnect-git
_pkgname=networkmanager-openconnect
pkgver=1.2.4.r100.g4d77d3e
pkgrel=1
pkgdesc="NetworkManager VPN plugin for OpenConnect"
url="https://wiki.gnome.org/Projects/NetworkManager"
arch=(x86_64)
license=(GPL)
depends=(libnm libsecret openconnect)
makedepends=(libnm-gtk intltool python git)
source=("git+https://git.gnome.org/browse/${_pkgname}")
install="${pkgname}".install
provides=("${_pkgname}")
conflicts=("${_pkgname}")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's,^[^0-9]*,,;s,\([^-]*-g\),r\1,;s,-,.,g'
}

prepare() {
  cd "${_pkgname}"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${_pkgname}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install
  echo 'u nm-openconnect - "NetworkManager OpenConnect"' |
      install -Dm644 /dev/stdin "${pkgdir}/usr/lib/sysusers.d/${_pkgname}.conf"
}
