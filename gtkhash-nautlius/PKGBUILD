# Maintainer: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Jan Böhringer <janboe@gmail.com>
# Contributor: Frédérik Paradis <fredy_14@live.fr>
# Contributor: GI_Jack <GI_Jack@hushmail.com>

pkgbase=gtkhash
pkgname=(gtkhash gtkhash-nautilus)
pkgver=1.1.1
pkgrel=1
pkgdesc="A GTK+ utility for computing message digests or checksums"
arch=('i686' 'x86_64' 'mips64el')
url="http://gtkhash.sourceforge.net/"
license=('GPL')
makedepends=('intltool' 'librsvg' 'libnautilus-extension')
depends=('dconf' 'nettle' 'gtk3')
source=("https://github.com/tristanheaven/gtkhash/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('a18c09a6af5b26c79bda81ab8a1835679fefbf2b87f1df4858c42ad45729f6bf')

build() {
  cd gtkhash-$pkgver

  ./configure --prefix=/usr --disable-schemas-compile --enable-gtkhash \
              --enable-linux-crypto --enable-nettle --with-gtk=3.0 \
              --enable-nautilus --disable-blake2
  make
}

package_gtkhash() {
  make -C gtkhash-$pkgver DESTDIR="$pkgdir/" install

  # remove filemanager plugins
  rm -rf "$pkgdir"/usr/lib
}

package_gtkhash-nautilus() {
  pkgdesc+=' (Nautilus filemanager plugin)'
  depends=('gtkhash' 'nautilus')

  make -C gtkhash-$pkgver/src/nautilus DESTDIR="$pkgdir/" install
  # remove other filemanager plugins
  rm -rf "$pkgdir"/usr/lib/{thunarx-2,caja,nemo}
}

