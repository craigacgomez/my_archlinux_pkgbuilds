# Maintainer: Brian BIdulock <bidulock@openss7.org>

pkgname=openconnect-git
_pkgname=openconnect
pkgver=7.08.r193.gc7140a50
pkgrel=1
pkgdesc="Open client for Cisco AnyConnect VPN"
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="http://www.infradead.org/openconnect.html"
depends=('libxml2' 'gnutls' 'libproxy' 'vpnc' 'krb5' 'lz4')
makedepends=('intltool' 'python2' 'git')
options=('!emptydirs')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
install="${pkgname}".install
source=("${_pkgname}::git://git.infradead.org/users/dwmw2/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's,^[^0-9]*,,;s,\([^-]*-g\),r\1,;s,-,.,g'
}

prepare() {
  cd "${_pkgname}"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${_pkgname}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --sbindir=/usr/bin \
      --disable-static
  make
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install
}
